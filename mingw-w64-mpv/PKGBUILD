# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: James Ross-Gowan <rossymiles@gmail.com>

_realname=mpv
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.39.0
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (mingw-w64)"
url="https://mpv.io/"
msys2_repository_url="https://github.com/mpv-player/mpv"
msys2_references=(
  "cpe: cpe:/a:mpv:mpv"
)
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
license=('spdx:GPL-2.0-or-later')
depends=("${MINGW_PACKAGE_PREFIX}-ffmpeg"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-libarchive"
         "${MINGW_PACKAGE_PREFIX}-libass"
         "${MINGW_PACKAGE_PREFIX}-libbluray"
         "${MINGW_PACKAGE_PREFIX}-libcaca"
         "${MINGW_PACKAGE_PREFIX}-libcdio"
         "${MINGW_PACKAGE_PREFIX}-libcdio-paranoia"
         "${MINGW_PACKAGE_PREFIX}-libdvdnav"
         "${MINGW_PACKAGE_PREFIX}-libdvdread"
         "${MINGW_PACKAGE_PREFIX}-libjpeg-turbo"
         "${MINGW_PACKAGE_PREFIX}-libplacebo"
         "${MINGW_PACKAGE_PREFIX}-luajit"
         "${MINGW_PACKAGE_PREFIX}-mujs"
         "${MINGW_PACKAGE_PREFIX}-rubberband"
         "${MINGW_PACKAGE_PREFIX}-shaderc"
         "${MINGW_PACKAGE_PREFIX}-spirv-cross"
         "${MINGW_PACKAGE_PREFIX}-uchardet"
        #  "${MINGW_PACKAGE_PREFIX}-vapoursynth"
         "${MINGW_PACKAGE_PREFIX}-vulkan"
         "${MINGW_PACKAGE_PREFIX}-openal"
         "${MINGW_PACKAGE_PREFIX}-SDL2"
         "winpty")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-python-docutils"
             "${MINGW_PACKAGE_PREFIX}-python-rst2pdf"
             "${MINGW_PACKAGE_PREFIX}-vulkan-headers")
optdepends=("${MINGW_PACKAGE_PREFIX}-yt-dlp: for video-sharing websites playback")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/mpv-player/${_realname}/archive/v${pkgver}.tar.gz")
sha256sums=('2ca92437affb62c2b559b4419ea4785c70d023590500e8a52e95ea3ab4554683')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  echo EXPORTS>libmpv/libmpv.def
#   cat libmpv/mpv.def>>libmpv/libmpv.def
  cat ../../ffmpeg.def>>libmpv/libmpv.def
  sed -ie "s#library('mpv', sources,#library('mpv', sources, vs_module_defs: 'libmpv/libmpv.def',#" meson.build
  sed -ie "s#spirv-cross-c-shared#spirv-cross-c#" meson.build
}

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  local LINK_ARGS="-Wl,--exclude-all-symbols -static -lsharpyuv -lvorbisfile"
  local C_ARGS="-DAL_LIBTYPE_STATIC -DPL_STATIC"

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]];then
    LINK_ARGS="$LINK_ARGS -lomp"
  else
    LINK_ARGS="$LINK_ARGS -lgomp"
  fi

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson setup \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --buildtype=plain \
      -Dlibmpv=true \
      -Djavascript=enabled \
      $([[ ${CARCH} == aarch64 ]] && echo "-Dgl=disabled") \
      -Db_lto=true \
      -Db_ndebug=true \
      -Dc_args="${C_ARGS}" \
      -Dc_link_args="${LINK_ARGS}" \
      -Dprefer_static=true \
      -Dpdf-build=enabled \
      -Dlua=enabled \
      -Djavascript=enabled \
      -Dsdl2=enabled \
      -Dlibarchive=disabled \
      -Dlibbluray=enabled \
      -Ddvdnav=enabled \
      -Duchardet=enabled \
      -Drubberband=enabled \
      -Dlcms2=enabled \
      -Dopenal=enabled \
      -Dspirv-cross=enabled \
      -Dvulkan=enabled \
      -Dvapoursynth=enabled \
      -Degl-angle=enabled \
      -Dcdda=enabled \
      -Dvaapi-win32=disabled \
      ../${_realname}-${pkgver}

  meson compile
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  DESTDIR="${pkgdir}" meson install

  # Move encoding-profiles.conf to share/doc alongside the example .conf files.
  # mpv doesn't search /etc for configuration on MinGW.
  mv "${pkgdir}${MINGW_PREFIX}/etc/mpv/"*.conf "${pkgdir}${MINGW_PREFIX}/share/doc/mpv/"

  # mpv needs winpty for key bindings to work on the terminal
  mv "${pkgdir}${MINGW_PREFIX}/bin/mpv.exe" "${pkgdir}${MINGW_PREFIX}/bin/mpv_exe"
  _exename=mpv
  echo '#!/usr/bin/env bash' > "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  echo 'export _started_from_console=yes' >> "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  echo '/usr/bin/winpty "$( dirname ${BASH_SOURCE[0]} )/'${_exename}'.exe" "$@"' >> "${pkgdir}${MINGW_PREFIX}/bin/${_exename}"
  mv "${pkgdir}${MINGW_PREFIX}/bin/mpv_exe" "${pkgdir}${MINGW_PREFIX}/bin/mpv.exe"
  strip ${pkgdir}${MINGW_PREFIX}/bin/*.exe
  strip ${pkgdir}${MINGW_PREFIX}/bin/*.dll
}
